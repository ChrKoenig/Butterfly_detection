# ------------------------------------------------------------------------------------- #
#                        COMPLEX MULTI SPECIES OCCUPANCY MODEL
# Random effect of year on occupancy intercept
# Additional term in detection submodel representing observer-related variation
# Effects of traits on detection intercept
# Community hyperpriors on species-level parameters
# --------------------------------------------------------------------------------------#

model{
  #          Priors            #
  # -------------------------- #
  
  ## 1. Detection model ##
  # Intercept with random observer effect
  for(o in 1:n_observer){
    alpha_null[o] ~ dnorm(mu_alpha_null, tau_alpha_null)
  }
  mu_alpha_null ~ dnorm(0, 0.0001) T(-12,12)
  tau_alpha_null ~ dgamma(0.01,0.01)
  
  # Trait effects on intercept
  for(p in 1:n_pred_det_traits){
    alpha_coef_traits[p] ~ ddexp(0, 0.0001) T(-12,12)
  }
  
  # Hyperpriors for slopes
  for(p in 1:n_pred_det_env) {
    mu_alpha_coef_env[p] ~ dnorm(0, 0.0001) T(-12,12)
    tau_alpha_coef_env[p] ~ dgamma(0.01,0.01)
  }
  
  # Species-level slopes
  for(i in 1:n_spec){
    for(p in 1:n_pred_det_env) {
      alpha_coef_env[i,p] ~ dnorm(mu_alpha_coef_env[p], tau_alpha_coef_env[p])
    }
  }
  
  ## 2. State model ##
  # Hyperpriors for intercept and slopes
  mu_beta_null ~ dnorm(0, 0.0001) T(-12,12)
  tau_beta_null ~ dgamma(0.01,0.01) 
  for(p in 1:n_pred_occ) {
    mu_beta_coef[p] ~ dnorm(0, 0.0001) T(-12,12)
    tau_beta_coef[p] ~ dgamma(0.01,0.01)
  }
  
  for(i in 1:n_spec){
    for(t in 1:n_year){
      beta_null[i,t] ~ dnorm(mu_beta_null, tau_beta_null)
    }
    for(p in 1:n_pred_occ){
      beta_coef[i,p] ~ dnorm(mu_beta_coef[p], tau_beta_coef[p])
    }
  }

  #        Likelihood          #
  # -------------------------- #
  for(i in 1:n_spec){
    for(j in 1:n_sites){
      logit(psi[i,j]) = beta_null[i,year[j]] + inprod(beta_coef[i,], x_state[j,])
      
      for(k in 1:n_visits){
        # Probability of observation in two visits (secondary samples)
        y[i,j,k] ~ dbin(mu[i,j,k], 2)
        mu[i,j,k] = z[i,j,k] * p[i,j,k]
        
        # occupancy state during visit k
        z[i,j,k] ~ dbern(psi[i,j])
        
        # detection probability
        logit(p[i,j,k]) = alpha_null[observer[i]] +
                          inprod(alpha_coef_traits, x_det_traits[i,]) +
                          alpha_coef_env[i,1] * x_det_elev[j] + 
                          alpha_coef_env[i,2] * pow(x_det_elev[j], 2) +
                          alpha_coef_env[i,3] * x_det_day[j,k] +
                          alpha_coef_env[i,4] * pow(x_det_day[j,k], 2)
      }
    }
  }
}
