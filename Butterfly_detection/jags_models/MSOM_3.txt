# ------------------------------------------------------------------------------------- #
#                        ENHANCED MULTI SPECIES OCCUPANCY MODEL
# Effects of traits on detection slope
# Community hyperpriors on species-level parameters
# --------------------------------------------------------------------------------------#

model{
  #          Priors            #
  # -------------------------- #
  ## 1. Detection model ##
  # Hyperpriors for intercept
  mu_alpha_null_l1 ~ dnorm(0, 0.001) # species specific intercept
  tau_alpha_null_l1 <- pow(sigma_alpha_null_l1, -2)
  sigma_alpha_null_l1 ~ dunif(0,100)

  # Hyperpriors for trait effects on slopes
  alpha_null_l2 ~ dnorm(0, 0.001)
  for(p in 1:n_pred_det_l2){
    alpha_coef_l2[p] ~ dnorm(0, 0.001) 
  }

  # Priors for slopes
  for (i in 1:n_spec){
    alpha_null_l1[i] ~ dnorm(mu_alpha_null_l1, tau_alpha_null_l1) T(-12,12)
    for(p in 1:n_pred_det_l1){
      alpha_coef_l1[i,p] ~ dnorm(mu_alpha_coef_l1[i,p], tau_alpha_coef_l1[i,p]) T(-12,12)
      mu_alpha_coef_l1[i,p] = alpha_null_l2 + inprod(alpha_coef_l2, x_det_traits[i,])
      tau_alpha_coef_l1[i,p] <- pow(sigma_alpha_coef_l1[i,p], -2)
      sigma_alpha_coef_l1[i,p] ~ dunif(0, 100)
    }
  }

  ## 2. State model ##
  # Hyperpriors for state model
  mu_beta_null ~ dnorm(0, 0.001) 
  tau_beta_null <- pow(sigma_beta_null, -2)
  sigma_beta_null ~ dunif(0,100)
  
  for(p in 1:n_pred_occ) {
    mu_beta_coef[p] ~ dnorm(0, 0.001)
    tau_beta_coef[p] <- pow(sigma_beta_coef[p], -2)
    sigma_beta_coef[p] ~ dunif(0,100)
  }

  # Priors for species-level coefficients
  for (i in 1:n_spec){
    beta_null[i] ~ dnorm(mu_beta_null, tau_beta_null) T(-12,12)
    for(p in 1:n_pred_occ) {
      beta_coef[i,p] ~ dnorm(mu_beta_coef[p], tau_beta_coef[p]) T(-12,12)
    }
  }

  #        Likelihood          #
  # -------------------------- #
  for(i in 1:n_spec){
    for(j in 1:n_sites){
      # probability of occupancy
      logit(psi[i,j]) = beta_null[i] + inprod(beta_coef[i,], x_state[j,])

      for(k in 1:n_visits){
        # probability of observation in two visits (secondary samples)
        y[i,j,k] ~ dbin(mu[i,j,k], 2)
        mu[i,j,k] = z[i,j,k] * p[i,j,k]

        # occupancy state during visit k
        z[i,j,k] ~ dbern(psi[i,j])

        # detection probability
        logit(p[i,j,k]) = alpha_null_l1[i] +
                          alpha_coef_l1[i,1] * x_det_elev[j] +
                          alpha_coef_l1[i,2] * pow(x_det_elev[j], 2) +
                          alpha_coef_l1[i,3] * x_det_day[j,k] +
                          alpha_coef_l1[i,4] * pow(x_det_day[j,k], 2)
      }
    }
  }
}
