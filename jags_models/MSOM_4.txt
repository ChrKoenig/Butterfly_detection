model{
  #          Priors            #
  # -------------------------- #
  # Hyperpriors for detection model
  mu_alpha_null ~ dnorm(0, 0.0001) T(-12,12)
  tau_alpha_null ~ dgamma(0.01,0.01)
  
  for(p in 1:n_pred_det_l2){
    alpha_coef_l2[p] ~ dnorm(0, 0.0001) T(-12,12)  
  }
  
  # Hyperpriors for state model
  mu_beta_null ~ dnorm(0, 0.0001) T(-12,12)
  tau_beta_null ~ dgamma(0.01,0.01) 
  for(p in 1:n_pred_occ) {
    mu_beta_coef[p] ~ dnorm(0, 0.0001) T(-12,12)
    tau_beta_coef[p] ~ dgamma(0.01,0.01)
  }
  
  # Priors for species-level coefficients
  for (i in 1:n_spec){
    alpha_null[i] ~ dnorm(mu_alpha_null, tau_alpha_null) T(-12,12)
    for(p in 1:n_pred_det_l1){
      alpha_coef_l1[i,p] ~ dnorm(mu_alpha_coef_l1[i,p], tau_alpha_coef_l1[i,p])
      mu_alpha_coef_l1[i,p] = inprod(alpha_coef_l2, x_det_traits[i,])
      tau_alpha_coef_l1[i,p] ~ dgamma(0.01,0.01)
    }
    ## 2. State model ##
    beta_null[i] ~ dnorm(mu_beta_null, tau_beta_null)
    for(p in 1:n_pred_occ) {
      beta_coef[i,p] ~ dnorm(mu_beta_coef[p], tau_beta_coef[p])
    }
  }
  
  #        Likelihood          #
  # -------------------------- #
  for(i in 1:n_spec){
    for(j in 1:n_sites){
      # probability of occupancy
      logit(psi[i,j]) = beta_null[i] + inprod(beta_coef[i,], x_state[j,]) # @GURU: There are no visit-specific environmental predictors, so no need to estimate multiple times, right?
      
      for(k in 1:n_visits){
        # probability of observation in two visits (secondary samples)
        y[i,j,k] ~ dbin(mu[i,j,k], 2) 
        mu[i,j,k] = z[i,j,k] * p[i,j,k] 
        
        # occupancy state during visit k
        z[i,j,k] ~ dbern(psi[i,j]) 
        
        # detection probability
        logit(p[i,j,k]) = alpha_null[i] +
                          alpha_coef_l1[i,1] * x_det_elev[j] + 
                          alpha_coef_l1[i,2] * pow(x_det_elev[j], 2) +
                          alpha_coef_l1[i,3] * x_det_day[j,k] +
                          alpha_coef_l1[i,4] * pow(x_det_day[j,k], 2)
      }
    }
  }
}